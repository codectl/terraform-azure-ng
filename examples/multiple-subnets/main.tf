module "naming" {
  source  = "codectl/naming/azure"
  version = "~> 0.1"

  suffix = ["demo", "dev"]
}

module "regions" {
  source  = "codectl/locations/azure"
  version = "~> 1.0"

  location = {
    primary = "westeurope"
  }
}

module "rg" {
  source  = "codectl/rg/azure"
  version = "~> 1.0"

  groups = {
    demo = {
      name     = module.naming.resource_group.name_unique
      location = module.regions.location.primary.name
    }
  }
}

module "network" {
  source  = "codectl/vnet/azure"
  version = "~> 1.0"


  vnet = {
    name                = module.naming.virtual_network.name
    location            = module.rg.groups.demo.location
    resource_group_name = module.rg.groups.demo.name
    address_space       = ["10.0.0.0/16"]
    dns_servers         = ["8.8.8.8", "7.7.7.7"]

    subnets = {
      sn1 = {
        address_prefixes = ["10.0.1.0/24"]
      }
      sn2 = {
        address_prefixes = ["10.0.2.0/24"]
      }
      sn3 = {
        address_prefixes = ["10.0.3.0/24"]
      }
    }
  }
}

module "public_ip" {
  source  = "codectl/pip/azure"
  version = "~> 1.0"

  public_ips = {
    pub1 = {
      name                = "${module.naming.public_ip.name}1"
      location            = module.rg.groups.demo.location
      resource_group_name = module.rg.groups.demo.name
      zones               = ["1", "2", "3"]
    }
  }
}

module "natgw" {
  source  = "codectl/ng/azure"
  version = "~> 1.0"

  nat_gateway = {
    name                = module.naming.nat_gateway.name
    location            = module.rg.groups.demo.location
    resource_group_name = module.rg.groups.demo.name
    zones               = ["1"]

    associations = {
      subnets = {
        sn1 = {
          subnet_id = module.network.subnets.sn1.id
        }
        sn2 = {
          subnet_id = module.network.subnets.sn2.id
        }
        sn3 = {
          subnet_id = module.network.subnets.sn3.id
        }
      }
      public_ips = {
        pub1 = {
          public_ip_address_id = module.public_ip.public_ips.pub1.id
        }
      }
    }
  }
}